:description: The `MATCH` clause is used to search for the pattern described in it.
include::https://raw.githubusercontent.com/neo4j-graphacademy/courses/main/asciidoc/ads/data-analysis.adoc[]

= MATCH

The `MATCH` clause enables you to define specific patterns that the database will search for within its graph structure.
The `MATCH` clause can specify the nodes, relationships, and properties in a pattern, allowing for queries that traverse the graph to retrieve relevant data.

[[match-example-graph]]
== Example graph

The following graph is used for the examples below:

image::graph_match_clause.svg[width="500",role="middle"]

To recreate the graph, run the following query against an empty Neo4j database:

[source, cypher, role=test-setup]
----
CREATE (charlie:Person:Actor {name: 'Charlie Sheen'}),
       (martin:Person:Actor {name: 'Martin Sheen'}),
       (michael:Person:Actor {name: 'Michael Douglas'}),
       (oliver:Person:Director {name: 'Oliver Stone'}),
       (rob:Person:Director {name: 'Rob Reiner'}),
       (wallStreet:Movie {title: 'Wall Street'}),
       (charlie)-[:ACTED_IN {role: 'Bud Fox'}]->(wallStreet),
       (martin)-[:ACTED_IN {role: 'Carl Fox'}]->(wallStreet),
       (michael)-[:ACTED_IN {role: 'Gordon Gekko'}]->(wallStreet),
       (oliver)-[:DIRECTED]->(wallStreet),
       (thePresident:Movie {title: 'The American President'}),
       (martin)-[:ACTED_IN {role: 'A.J. MacInerney'}]->(thePresident),
       (michael)-[:ACTED_IN {role: 'President Andrew Shepherd'}]->(thePresident),
       (rob)-[:DIRECTED]->(thePresident)
----

[[find-nodes]]
== Find nodes

The `MATCH` clause allows you to specify node patterns of varying complexity to retrieve from a graph.
For more information about finding node patterns, see xref:patterns/fixed-length-patterns#node-patterns[Patterns -> Node patterns].

[[find-all-nodes]]
=== Find all nodes

By specifying a pattern with a single node and no labels, all nodes in the graph will be returned.

.Find all nodes in a graph
[source, cypher]
----
MATCH (n)
RETURN n
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| n
| (:Person {"name":"Charlie Sheen"})
| (:Person {"name":"Martin Sheen"})
| (:Person {"name":"Michael Douglas"})
| (:Person {"name":"Oliver Stone"})
| (:Person {"name":"Rob Reiner"})
| (:Movie {"title":"Wall Street"})
| (:Movie {"title":"The American President"})
1+d| Rows: 7
|===


[[find-nodes-with-label]]
=== Find nodes with a specific label

.Find all nodes with the `Movie` label
[source, cypher]
----
MATCH (movie:Movie)
RETURN movie.title
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| movie.title
| "Wall Street"
| "The American President"
1+d| Rows: 2
|===


[[match-node-label-expressions]]
=== MATCH using node label expressions

.Node pattern using the `OR` (`|`) label expression
// tag::clauses_match_label_expression_or[]
[source, cypher]
----
MATCH (n:Movie|Person)
RETURN n.name AS name, n.title AS title
----
// end::clauses_match_label_expression_or[]


.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| name | title
| "Charlie Sheen" | <null>
| "Martin Sheen" | <null>
| "Michael Douglas" | <null>
| "Oliver Stone" | <null>
| "Rob Reiner" | <null>
| <null> | "Wall Street"
| <null> | "The American President"
2+d| Rows: 7
|===

.Node pattern using negation (`!`) label expression
// tag::clauses_match_label_expression_negation[]
[source, cypher]
----
MATCH (n:!Movie)
RETURN labels(n) AS label, count(n) AS labelCount
----
// end::clauses_match_label_expression_negation[]


[NOTE]
The above query uses the xref:functions/list.adoc#functions-labels[`labels()`] and xref:functions/aggregating.adoc#functions-count[`count()`] functions.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| label | labelCount
| ["Person", "Actor"] | 3
| ["Person", "Director"] | 2

2+d| Rows: 2
|===

For a list of all label expressions supported by Cypher, see xref:patterns/reference.adoc#label-expressions[Patterns -> Label expressions].

[[find-relationships]]
== Find relationships

The `MATCH` clause allows you to specify relationship patterns of varying complexity to retrieve from a graph.
Unlike a node pattern, a relationship pattern cannot be used in a `MATCH` clause without node patterns at both ends.
For more information about relationship patterns, see xref:patterns/fixed-length-patterns#relationship patterns[Patterns -> Relationship patterns].

[NOTE]
Relationships will only be matched once inside a single pattern.
Read more about this behavior in the section on xref::patterns/reference.adoc#graph-patterns-rules-relationship-uniqueness[relationship uniqueness].

[[empty-relationship-patterns]]
=== Empty relationship patterns

By applying `--`, a pattern will be matched for a relationship with any direction and without any filtering on relationship types or properties.

.Find connected nodes using an empty relationship pattern
[source, cypher]
----
MATCH (:Person {name: 'Oliver Stone'})--(n)
RETURN n AS connectedNodes
----

.Result
[source, role="queryresult",options="header,footer",cols="1*<m"]
|===
| connectedNodes
| (:Movie {title: "Wall Street"})
1+d| Rows: 1
|===

[[directed-relationship-patterns]]
=== Directed relationship patterns

The direction of a relationship in a pattern is indicated by arrows: `+-->+` or `+<--+`.

.Find all nodes connected to `Oliver Stone` by an outgoing relationship.
[source, cypher]
----
MATCH (:Person {name: 'Oliver Stone'})-->(movie:Movie)
RETURN movie.title AS movieTitle
----

.Result
[source, role="queryresult",options="header,footer",cols="1*<m"]
|===
| movieTitle
| "Wall Street"
1+d| Rows: 1
|===

[[relationship-variables]]
=== Relationship variables

It is possible to introduce a variable to a pattern, either for filtering on relationship properties or to return a relationship.

.Find the types of an aliased relationship
[source, cypher]
----
MATCH (:Person {name: 'Oliver Stone'})-[r]->()
RETURN type(r) AS relType
----

[NOTE]
The above query uses the xref:functions/scalar.adoc#functions-type[`type()` function].

.Result
[source, role="queryresult",options="header,footer",cols="1*<m"]
|===
| relType
| "DIRECTED"
1+d| Rows: 1
|===

[[undirected-relationships]]
=== MATCH on an undirected relationship

When a pattern contains a bound relationship, and that relationship pattern does not specify direction, Cypher will match the relationship in both directions.

.Relationship pattern without direction
[source, cypher]
----
MATCH (a)-[:ACTED_IN {role: 'Bud Fox'}]-(b)
RETURN a, b
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| a | b

| (:Movie {"title":"Wall Street"})
| (:Person {"name":"Charlie Sheen"})

| (:Person {"name":"Charlie Sheen"})
| (:Movie {"title":"Wall Street"})

2+d| Rows: 2
|===

[[match-on-relationship-type]]
=== Filter on relationship types

It is possible to specify the type of a relationship in a relationship pattern by using a colon (`:`) before the relationship type.

.Relationship pattern filtering on the `ACTED_IN` relationship type
[source, cypher]
----
MATCH (:Movie {title: 'Wall Street'})<-[:ACTED_IN]-(actor:Person)
RETURN actor.name AS actor
----

.Result
[source, role="queryresult",options="header,footer",cols="1*<m"]
|===
| actor
| "Michael Douglas"
| "Martin Sheen"
| "Charlie Sheen"
1+d| Rows: 3
|===

[[match-relationship-type-expressions]]
=== MATCH using relationship type expressions

It is possible to match a pattern containing one of several relationship types using the `OR` symbol, `|`.

.Relationship pattern including either `ACTED_IN` or `DIRECTED` relationship types
// tag::clauses_match_type_expression_or[]
[source, cypher]
----
MATCH (:Movie {title: 'Wall Street'})<-[:ACTED_IN|DIRECTED]-(person:Person)
RETURN person.name AS person
----
// end::clauses_match_type_expression_or[]


.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| person
| "Oliver Stone"
| "Michael Douglas"
| "Martin Sheen"
| "Charlie Sheen"
1+d| Rows: 4
|===

As relationships can only have exactly one type each, `()-[:A&B]->()` will never match a relationship.

For a list of all relationship type expressions supported by Cypher, see xref:patterns/reference.adoc#label-expressions[Patterns -> Label expressions].

[[multiple-relationships]]
=== Find multiple relationships

A graph pattern can contain several relationship patterns.

.Graph pattern including several relationship patterns
[source, cypher]
----
MATCH (:Person {name: 'Charlie Sheen'})-[:ACTED_IN]->(movie:Movie)<-[:DIRECTED]-(director:Person)
RETURN movie.title AS movieTitle, director.name AS director
----

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| movieTitle | director
| "Wall Street" | "Oliver Stone"
2+d| Rows: 1
|===

[[where-predicates]]
== MATCH with WHERE predicates

The `MATCH` clause is often paired with a `WHERE` sub-clause, which adds predicates to refine the patterns, making them more specific.
These predicates are part of the pattern itself, not just filters applied after matching.
Thus, always place the `WHERE` clause with its corresponding `MATCH` clause.

.Simple `WHERE` predicate
[source, cypher]
----
MATCH (charlie:Person)-[:ACTED_IN]->(movie:Movie)
WHERE charlie.name = 'Charlie Sheen'
RETURN movie.title AS movieTitle
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| movieTitle
| "Wall Street"
1+d| Rows: 1
|===

.More complex `WHERE` predicate
[source, cypher]
----
MATCH (martin:Person)-[:ACTED_IN]->(movie:Movie)
WHERE martin.name = 'Martin Sheen' AND NOT EXISTS {
    MATCH (movie)<-[:DIRECTED]-(director:Person {name: 'Oliver Stone'})
}
RETURN movie.title AS movieTitle
----

[NOTE]
The above query uses an xref:subqueries/existential.adoc[`EXISTS` subquery].

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| movieTitle
| "The American President"
1+d| Rows: 1
|===

For more information, see the xref:clauses/where.adoc[`WHERE`] page.

[[parameters]]
== MATCH with parameters

The `MATCH` clause can be used with parameters.

.Parameters
[source, parameters]
----
{
  "movieTitle": "Wall Street",
  "actorRole": "Fox"
}
----

.Find nodes using paramters
[source, cypher]
----
MATCH (:Movie {title: $movieTitle})<-[r:ACTED_IN]-(p:Person)
WHERE r.role CONTAINS $actorRole
RETURN p.name AS actor, r.role AS role
----

[NOTE]
The above query uses the xref:syntax/operators.adoc#query-operator-comparison-string-specific[`CONTAINS` operator].

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| actor | role

| "Charlie Sheen" | "Bud Fox"
| "Martin Sheen"  | "Carl Fox"

2+d|Rows: 2
|===

For more information about how to set parameters, see xref:syntax/parameters.adoc[Syntax -> Parameters].

[[find-paths]]
== Find paths

The `MATCH` clause can also be used to bind whole paths to variables.

.Find all paths matching a pattern
[source, cypher]
----
MATCH path = ()-[:ACTED_IN]->(movie:Movie)
RETURN path
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| path
| (:Person {name: "Charlie Sheen"})-[:ACTED_IN {role: "Bud Fox"}]->(:Movie {title: "Wall Street"}) 
| (:Person {name: "Martin Sheen"})-[:ACTED_IN {role: "Carl Fox"}]->(:Movie {title: "Wall Street"})
| (:Person {name: "Martin Sheen"})-[:ACTED_IN {role: "A.J. MacInerney"}]->(:Movie {title: "The American President"})
| (:Person {name: "Michael Douglas"})-[:ACTED_IN {role: "Gordon Gekko"}]->(:Movie {title: "Wall Street"})
| (:Person {name: "Michael Douglas"})-[:ACTED_IN {role: "President Andrew Shepherd"}]->(:Movie {title: "The American President"})
1+d| Rows: 5
|===


.Find paths matching a pattern including a `WHERE` predicate
[source, cypher]
----
MATCH path = (:Person)-[:ACTED_IN]->(movie:Movie)<-[:DIRECTED]-(:Person)
WHERE movie.title = 'Wall Street'
RETURN path
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| path
| (:Person {name: "Charlie Sheen"})-[:ACTED_IN {role: "Bud Fox"}]->(:Movie {title: "Wall Street"})<-[:DIRECTED]-(:Person {name: "Oliver Stone"})
| (:Person {name: "Martin Sheen"})-[:ACTED_IN {role: "Carl Fox"}]->(:Movie {title: "Wall Street"})<-[:DIRECTED]-(:Person {name: "Oliver Stone"})
| (:Person {name: "Michael Douglas"})-[:ACTED_IN {role: "Gordon Gekko"}]->(:Movie {title: "Wall Street"})<-[:DIRECTED]-(:Person {name: "Oliver Stone"})
1+d| Rows: 3
|===

For more information about how `MATCH` is used to find patterns of varying complexity (including xref:patterns/variable-length-patterns.adoc#quantified-path-patterns[quantified path patterns], xref:patterns/variable-length-patterns.adoc#quantified-relationships[quantified relationships], and the xref:patterns/shortest-paths.adoc[shortest paths] between nodes), see the section on xref::patterns/index.adoc[Patterns].

== Multiple MATCH clauses, the WITH clause, and clause composition

In Cypher, the behavior of a query is defined by its clauses.
Each clause takes the current graph state and a table of intermediate results, processes them, and passes the updated graph state and results to the next clause.
The first clause starts with the graph's initial state and an empty table, while the final clause produces the query result.

.Chaining consecutive `MATCH` clauses
[source, cypher]
----
MATCH (:Person {name: 'Martin Sheen'})-[:ACTED_IN]->(movie:Movie) // <1>
MATCH (director:Person)-[:DIRECTED]->(movie) // <2>
RETURN director.name AS director, movie.title AS movieTitle
----
<1> The result of the first `MATCH` clause is the variable `movie` which holds all the `Movies` that `Martin Sheen` has `ACTED_IN`.
<2> The second `MATCH` clause uses the `movie` variable to find any `Person` node with a `DIRECTED` relationship to those `Movie` nodes that `Martin Sheen` has `ACTED_IN`.

.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| director | movieTitle

| "Oliver Stone" | "Wall Street"
| "Rob Reiner"   | "The American President"

2+d| Rows: 2
|===

A variable can be implicitly carried over to the following clause by being referenced in another operation.
A variable can also be explicitly passed to the following clause using the xref:clauses/with.adoc[`WITH`] clause.
If a variable is neither implicitly nor explicitly carried over to its following clause, it will be discarded and is not available for reference later in the query.

.Using `WITH` and multiple `MATCH` clauses
[source, cypher]
----
MATCH (actors:Person)-[:ACTED_IN]->(movies:Movie) // <1>
WITH actors, count(movies) AS movieCount // <2>
ORDER BY movieCount DESC
LIMIT 1 // <3>
MATCH (actors)-[:ACTED_IN]->(movies) // <4>
RETURN actors.name AS actor, movieCount, collect(movies.title) AS movies
----
<1> The `Person` and `Movie` nodes matched in this step are stored in variables, which are then passed on to the second row of the query.
<2> The `movies` variable is implicitly imported by its occurrence in the `count()` function.
The `WITH` clause explicitly imports the `actors` variable.
<3> An xref:clauses/order-by.adoc[`ORDER BY`] clause orders the results by `movieCount` in descending order, ensuring that the `Person` with the highest number of movies appears at the top, and xref:clauses/limit.adoc[`LIMIT] 1` ensures that all other `Person` nodes are discarded.
<4> The second `MATCH` clause finds all `Movie` nodes associated with the `Person` nodes currently bound to the `actors` variable.

[NOTE]
The above query uses the xref:functions/aggregating.adoc#functions-collect[`collect()` function].

.Result
[role="queryresult",options="header,footer",cols="3*<m"]
|===
| actor | movieCount | movies

| "Martin Sheen" | 2 | ["Wall Street", "The American President"]
3+d| Rows: 1

|===

For more information about how Cypher queries work, see xref:clauses/clause-composition.adoc[].

[role=label--new-5.26]
[[dynamic-match]]
== MATCH using dynamic node labels and relationship types

Node labels and relationship types can be referenced dynamically in expressions, parameters, and variables when matching nodes and relationships.
This allows for more flexible queries and mitigates the risk of Cypher injection.
(For more information about Cypher injection, see link:https://neo4j.com/developer/kb/protecting-against-cypher-injection/[Neo4j Knowledge Base -> Protecting against Cypher injection]).

.Syntax for matching node labels dynamically
[source, syntax]
----
MATCH (n:$(<expr>))
MATCH (n:$any(<expr>))
MATCH (n:$all(<expr>))
----

[NOTE]
`MATCH (n:$all(<expr>))` is functionally equivalent to `MATCH (n:$(<expr>))`.

.Syntax for matching relationship types dynamically
[source, syntax]
----
MATCH ()-[r:$(<expr>))]->()
MATCH ()-[r:$any(<expr>)]->()
MATCH ()-[r:$all(<expr>))]->()
----

The expression must evaluate to a `STRING NOT NULL | LIST<STRING NOT NULL> NOT NULL` value.
If you use a `LIST<STRING>` with more than one item in a relationship pattern with dynamic relationship types, no results will be returned.
This is because a relationship can only have exactly one type.

[NOTE]
Queries using dynamic values may not be as performant as those using static values.
This is because the xref:planning-and-tuning/execution-plans.adoc[Cypher planner] uses statically available information when planning queries to determine whether to use an xref:indexes/search-performance-indexes/overview.adoc[index] or not, and this is not possible when using dynamic values.

.Match labels dynamically
[source, cypher]
----
WITH ["Person", "Director"] AS labels
MATCH (directors:$(labels))
RETURN directors
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| directors

| (:Person:Director {name: "Oliver Stone"})
| (:Person:Director {name: "Rob Reiner"})

1+d|Rows: 2
|===

.Match nodes dynamically using `any()`
[source, cypher]
----
MATCH (n:$any(["Movie", "Actor"]))
RETURN n AS nodes
----

.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| nodes

| (:Person:Actor {name: "Charlie Sheen"})
| (:Person:Actor {name: "Martin Sheen"})
| (:Person:Actor {name: "Michael Douglas"})
| (:Movie {title: "Wall Street"})
| (:Movie {title: "The American President"})

1+d|Rows: 5
|===


.Parameter
[source, parameters]
----
{
  "label": "Movie"
}
----

.Match nodes dynamically using a parameter
// tag::clauses_match_dynamic_match_parameter[]
[source, cypher]
----
MATCH (movie:$($label))
RETURN movie.title AS movieTitle
----
// end::clauses_match_dynamic_match_parameter[]


.Result
[role="queryresult",options="header,footer",cols="1*<m"]
|===
| movieTitle

| "Wall Street"
| "The American President"

1+d|Rows: 2
|===


.Match relationships dynamically using a variable
// tag::clauses_match_dynamic_match_variable[]
[source, cypher]
----
CALL db.relationshipTypes()
YIELD relationshipType
MATCH ()-[r:$(relationshipType)]->()
RETURN relationshipType, count(r) AS relationshipCount
----
// end::clauses_match_dynamic_match_variable[]


.Result
[role="queryresult",options="header,footer",cols="2*<m"]
|===
| relationshipType | relationshipCount

| "ACTED_IN" | 5
| "DIRECTED" | 2

2+d|Rows: 2
|===

